plugins {
    kotlin("jvm")
    id("org.wisepersist.gwt") version "1.1.18"
    id("java")
    id("war")
}

group = "com.gcgenome"
version = "1.0"

dependencies {
    implementation("com.gcgenome:gateway-api:1.0")
    implementation(libs.bundles.gwt)
    compileOnly(libs.gwt.dev)
    implementation("net.sayaya:ui:4.1")
    implementation(libs.lombok)
    annotationProcessor(libs.lombok)
}
val lombok = project.configurations.annotationProcessor.get().filter { it.name.startsWith("lombok") }.single()
tasks {
    withType<Delete> { doFirst { delete("build/") } }
    withType<JavaCompile> {
        options.encoding = "UTF-8"
    }
    gwt {
        gwt.modules = listOf("com.gcgenome.lims.Sample")
        minHeapSize = "1024M"
        maxHeapSize = "2048M"
        sourceLevel = "auto"
    }
    compileGwt {
        extraJvmArgs = listOf("-XX:ReservedCodeCacheSize=512M","-javaagent:${lombok}=ECJ")
    }
    gwtDev {
        extraJvmArgs = listOf("-XX:ReservedCodeCacheSize=512M","-javaagent:${lombok}=ECJ")
        port = 9629
        codeServerPort = 9630
        //war = file("src/main/webapp")
    }
    register<Copy>("copyWebResources") {
        dependsOn(build)
        from(zipTree("build/libs/sample-project-static.war")) {
            include("com.gcgenome.lims.sample/*.js")
            include("css/sample.css")
            include("sample.html")
            includeEmptyDirs = false
        }
        into("build/static")
    }
    withType<War> {
        archiveFileName.set("sample-project-static.war")
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    getByName<Test>("test") {
        useJUnitPlatform()
    }
}